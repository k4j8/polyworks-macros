version "5.0"
#=======================================================
# ---- InnovMetric Software Inc.
# ---- Module  :    PolyWorks|Inspector
# ---- Version :    2018 IR9 (build 6181)
# ---- Date    :    Monday, February 25, 2019 - 10:27:25
#-------------------------------------------------------

# Created by Kyle Johnston on 2019-02-25
# Last update: 2019-04-30

# Create snapshot and table (control view) for all control views and name the resulting snapshot and table by the control view's name


# Get control view names
DECLARE control_view_names
MEASURE CONTROL_VIEW NAME ALL GET ( control_view_names )

# Initialize variables
DECLARE i 1
DECLARE j
DECLARE item_name
DECLARE item_name_start

# Loop through each control view
WHILE $i <= SIZE( control_view_names)

    # Create snapshot and table
    MEASURE CONTROL_VIEW REPORT_ITEM CREATE SNAPSHOT_AND_TABLE ( $control_view_names[$i] )

    # Find last report item and work upwards be decreasing j by 1
    TREEVIEW REPORT_ITEM COUNT GET ( j )

    # Ignore footer if present
    TREEVIEW REPORT_ITEM NAME GET ( $j, item_name )
    IF $item_name == "Footer"
        --j
    ENDIF

    # Rename table
    TREEVIEW REPORT_ITEM NAME GET ( $j, item_name )
    MACRO STRING SUBSTRING ( $item_name, 1, 12, item_name_start )
    IF $item_name_start == "Control View"
        TREEVIEW REPORT_ITEM NAME ( $j, "${control_view_names[$i]} - Table" )
        --j
    ELSE
        MACRO ECHO ( "EXIT: Expected Control View (table) item but found ${item_name} instead" )
        MACRO END ( "Error" )
    ENDIF

    # Rename snapshot
    TREEVIEW REPORT_ITEM NAME GET ( $j, item_name )
    MACRO STRING SUBSTRING ( $item_name, 1, 8, item_name_start )
    IF $item_name_start == "Snapshot"
        TREEVIEW REPORT_ITEM NAME ( $j, "${control_view_names[$i]} - Snapshot" )
    ELSE
        MACRO ECHO ( "EXIT: Expected Snapshot item but found ${item_name} instead" )
        MACRO END ( "Error" )
    ENDIF

    # Next control view
    ++i
ENDWHILE
--i

# Report results to user
MACRO PAUSE ( "Marco complete", \
    "Snapshots and tables have been created and renamed for ${i} control view(s)." )