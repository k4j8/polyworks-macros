version "5.0"
#========================================================
# ---- InnovMetric Software Inc.
# ---- Module  :    PolyWorks|Inspector
# ---- Version :    2018 IR6 (build 5240)
# ---- Date    :    Wednesday, January 2, 2019 - 12:50:45
#--------------------------------------------------------

# Created by Kyle Johnston on 2019-01-07
# Last update: 2019-08-30

# Replaces text in selected objects for features, surface points, groups, and custom measurements


# Get text to find and replace from user
DECLARE find_text
DECLARE replace_text
MACRO INPUT MULTIPLE_PARAMETERS ( "Find and replace", "Enter text for find and replace", {"string", "Find what:", "", "string", "Replace with:", "" }, find_text, replace_text )

IF $find_text == "0"
    MACRO ECHO ( "EXIT: Macro cancelled by user." )
    MACRO END ( )
ENDIF

# Get objects to run find and replace on
DECLARE features
TREEVIEW FEATURE NAME SELECTED GET ( features )
DECLARE surf_pts
TREEVIEW COMPARISON_POINT NAME SELECTED GET ( surf_pts )
DECLARE groups
TREEVIEW OBJECT_GROUP NAME SELECTED GET ( groups )
DECLARE custom
TREEVIEW CUSTOM_MEASUREMENT NAME SELECTED GET ( custom )


# Replace text

DECLARE i
DECLARE index
DECLARE new_name

SET i 1
WHILE $i <= SIZE ( features )
    TREEVIEW FEATURE INDEX GET ( $features[$i], index )
    MACRO STRING REPLACE ( $features[$i], $find_text, $replace_text, 1, , new_name )
    IF $new_name != 0 THEN
        TREEVIEW FEATURE NAME ( $index, $new_name )
        ++i
    ENDIF
ENDWHILE

SET i 1
WHILE $i <= SIZE ( surf_pts )
    TREEVIEW COMPARISON_POINT INDEX GET ( $surf_pts[$i], index )
    MACRO STRING REPLACE ( $surf_pts[$i], $find_text, $replace_text, 1, , new_name )
    IF $new_name != 0 THEN
        TREEVIEW COMPARISON_POINT NAME ( $index, $new_name )
        ++i
    ENDIF
ENDWHILE

SET i 1
WHILE $i <= SIZE ( groups )
    MACRO STRING REPLACE ( $groups[$i], $find_text, $replace_text, 1, , new_name )
    TREEVIEW OBJECT_GROUP PROPERTIES NAME ( $new_name, $groups[$i] )
    ++i
ENDWHILE

SET i 1
WHILE $i <= SIZE ( custom )
    TREEVIEW CUSTOM_MEASUREMENT INDEX GET ( $custom[$i], index )
    MACRO STRING REPLACE ( $custom[$i], $find_text, $replace_text, 1, , new_name )
    TREEVIEW CUSTOM_MEASUREMENT NAME ( $index, $new_name )
    ++i
ENDWHILE
